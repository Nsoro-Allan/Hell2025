#version 430
#include "../common/lighting.glsl"
#include "../common/post_processing.glsl"
#include "../common/types.glsl"

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;
layout(rgba16f, binding = 0) uniform image2D lightingImage;
layout(binding = 1) uniform sampler2D fogTexture;

readonly restrict layout(std430, binding = 1) buffer rendererDataBuffer {
    RendererData rendererData;
};

// 0..1 hash, decent for dithering tests (not true blue noise but might be enough to kill banding)
float InterleavedGradientNoise(vec2 p) {
    return fract(52.9829189 * fract(dot(p, vec2(0.06711056, 0.00583715))));
}

// zero-mean triangular-ish noise in [-1, 1]
float DitherTri(ivec2 px) {
    float a = InterleavedGradientNoise(vec2(px));
    float b = InterleavedGradientNoise(vec2(px) + vec2(17.0, 9.0));
    return (a + b) - 1.0;
}

void main() {

    if (rendererData.rendererOverrideState == 4) {
        return;
    }

    ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);
    ivec2 resolution = imageSize(lightingImage);

    if (pixelCoords.x >= resolution.x || pixelCoords.y >= resolution.y) {
        return;
    }

    vec2 uv_screenspace = (vec2(pixelCoords) + 0.5) / vec2(resolution);

    vec4 fogColor = texture(fogTexture, uv_screenspace);
    vec4 lightingColor = imageLoad(lightingImage, pixelCoords);

    // Composite
    vec3 outRGB = mix(lightingColor.rgb, fogColor.rgb + lightingColor.rgb * fogColor.a, fogColor.a);

    // Dither test hack: add tiny noise after composite
    // Start with 1/255.0, try 2/255, 4/255 if you need more.
    //float ditherStrength = 2.0 / 255.0;
    //outRGB += DitherTri(pixelCoords) * ditherStrength; // DOES NOT WORK

    vec4 finalColor = vec4(outRGB, lightingColor.a);
    imageStore(lightingImage, pixelCoords, finalColor);
}
